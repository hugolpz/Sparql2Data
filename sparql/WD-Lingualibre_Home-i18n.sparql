# List from https://translatewiki.net/wiki/Translating:Lingua_Libre_SignIt#sortable:3=desc
# 2023.07.26 https://lingualibre.org/wiki/Help:SPARQL_for_maintenance#.E2.9C.85_Languages_iso_from_SignIt_.E2.86.92_relevant_data
SELECT ?wdQid ?wikimediaCode ?i18nCode ?labelEN 
  (SAMPLE(?nativeLabel) as ?labelNative)
WHERE {
    # languages & scripts
    VALUES ?i18nCode { 
     "ar" "ba" "ban" "be" "bg" "blk" "bn" "br" "ca" "de" 
     "dtp" "el" "eo" "es" "eu" "fa" "fr" "frc" "ga" "gl" 
     "he" "hi" "hyw" "ig" "it" "ja" "kaa" "ko" "lfn" "lmo" 
     "mad" "mk" "ml" "mnw" "ms" "my" "nb" "oc" "pl" "pnb" 
     "pt" "ro" "ru" "sat" "scn" "sdc" "shn" "sk" "sv" "ta" 
     "te" "tg" "th" "tly" "tr" "tt" "uk" "uz" "vi" "wls"
     "yue" "zgh" "zh"
 }
    # Scripts, not languages
    VALUES ?i18nScript { "kk-cyrl" "zh-yue" "zh-hans" "zh-hant" }
  
    { ?langId wdt:P218 ?i18nCode. } # has for iso 639-1 code
    UNION {?langId wdt:P219 ?i18nCode. } # has for iso 639-2 code
    UNION {?langId wdt:P220 ?i18nCode. } # has for iso 639-3 code
    UNION {?langId wdt:P305 ?i18nCode. } # has for isoIETF code
  
  #  UNION {?langId wdt:P31 wd:Q1149626 ; # has for instance written language
  #                 wdt:P424 ?i18nScript ; # has for wikimedia code
  #                 rdfs:label ?labelEN . }

    # UNION { ?langId wdt:P424 ?i18nCode. }  # has for wikimedia code

    OPTIONAL { ?langId wdt:P424 ?wikimediaCode. }
    OPTIONAL { ?langId wdt:P1705 ?native. }
            # wdt:P10369 ?lingualibreQid ;
    ?langId rdfs:label ?labelEN .
  FILTER ( lang(?labelEN) = "en" )
  BIND(REPLACE(STR(?langId), ".*/Q", "Q") AS ?wdQid)
  BIND(CONCAT(UCASE(SUBSTR(?native, 1 , 1 )), SUBSTR(?native, 2 )) AS ?nativeLabel)
}
GROUP BY ?wdQid ?wikimediaCode ?i18nCode ?labelEN
ORDER BY ASC(?i18nCode)
