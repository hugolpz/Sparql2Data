# MediaWiki:LanguagesGenderData-recordings.js
SELECT 
    ?languageLabel ?wikidata ?iso 
    ?records ?recordsMale ?recordsFemale
    (?records-COALESCE(?recordsMale, 0)-COALESCE(?recordsFemale,0) as ?recordsOthers)
    (ROUND(1000*?recordsFemale/(?recordsFemale+?recordsMale))/10 AS ?recordsGenderPercent)
WITH {
  SELECT ?language ?languageLabel ?wikidata ?iso (COUNT(?record) as ?records) {
      ?language prop:P12 ?wikidata .   # Assign value: P12 'wikidata id' into ?wikidata
      ?record prop:P2 entity:Q2 .     # Filter: P2 'instance of' is Q2 'record'
      ?record prop:P4 ?language .     # Assign value: P4 'language' into ?language
      
      OPTIONAL { ?language prop:P13 ?iso . } # Assign value: P13 'iso639-3' into ?iso
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en" }
	}
	GROUP BY ?language ?languageLabel ?wikidata ?iso
} AS %base
WITH {
  SELECT ?language ?languageLabel ?iso ?genderLabel 
    (COUNT(DISTINCT ?record) AS ?recordsFemale) {
  INCLUDE %base
  ?females prop:P8 entity:Q17 ;  # Filter
          prop:P8 ?gender . # Assign value: P8 'gender' into ?gender
      
   ?record prop:P5 ?females ; # Assign value: P5 'speaker' into ?females
           prop:P4 ?language . # Filter
    
   } GROUP BY ?language ?languageLabel ?iso ?genderLabel
} AS %females
WITH {
  SELECT ?language ?languageLabel ?iso ?genderLabel 
    (COUNT(DISTINCT ?record) AS ?recordsMale) {
  INCLUDE %base
  ?males prop:P8 entity:Q16 ; 
          prop:P8 ?gender .
  ?record prop:P5 ?males ; # Assign value: P5 'speaker' into variable ?speakerQid
          prop:P4 ?language

  } GROUP BY ?language ?languageLabel ?iso ?genderLabel
} AS %males
{
  INCLUDE %base
  OPTIONAL { INCLUDE %females }
  OPTIONAL { INCLUDE %males }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
GROUP BY ?languageLabel ?wikidata ?iso ?records ?recordsMale ?recordsFemale
ORDER BY ASC(?languageLabel )
