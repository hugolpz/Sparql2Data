# Lists users with their locations and their numbers of records.
# Source: https://lingualibre.org/index.php?title=LinguaLibre:Stats/Speakers&diff=782961&oldid=782826
# Source: https://w.wiki/5ZkA
PREFIX ll:<https://lingualibre.org/entity/>
PREFIX llt:<https://lingualibre.org/prop/direct/>
SELECT 
?itemLabel 
?wikidata 
(?info AS ?geocoordinates) 
?records 
(SAMPLE(?tag) AS ?layer)
WITH {
  SELECT *
  WHERE {
    SERVICE <https://lingualibre.org/sparql> {
      SELECT ?itemLabel ?wikidata (COUNT(?record) AS ?records)
      {
        ?item llt:P2 ll:Q3 ;
                 rdfs:label ?itemLabel;
                 llt:P14 ?wikidata .
        ?record llt:P5 ?item .
        FILTER (LANG(?itemLabel)='en')
        FILTER (regex(?wikidata, '^Q'))
      } GROUP BY ?itemLabel ?wikidata
    }
  }
} AS %infoWikidataId
WHERE {
  INCLUDE %infoWikidataId
  BIND (URI(CONCAT("http://www.wikidata.org/entity/", ?wikidata)) AS ?infoURL)
  SERVICE <https://query.wikidata.org/sparql> { 
    SELECT * { 
      ?infoURL wdt:P625 ?info .
    } 
  }
  BIND(
    IF(?records < 10, "<10",
    IF(?records < 1000, "10-1,000",
    IF(?records < 5000, "1k-5k",
    IF(?records < 25000, "5k-25k",
    IF(?records < 50000, "25k-50k",
    ">50k")))))
    AS ?tag).
}
GROUP BY ?itemLabel ?wikidata ?info ?records
ORDER BY DESC (?records)
